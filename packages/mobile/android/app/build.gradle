apply plugin: "com.android.application"
apply plugin: "com.facebook.react"
apply plugin: "com.google.gms.google-services"
apply plugin: "com.google.firebase.crashlytics"

react {
    root = file("../../")
    reactNativeDir = file("../../../../node_modules/react-native")
    codegenDir = file("../../../../node_modules/react-native-codegen")
    hermesCommand = "/Volumes/FInchRecovery/finch-app/node_modules/react-native/sdks/hermesc/%OS-BIN%/hermesc"
}

def enableProguardInReleaseBuilds = true
def jscFlavor = 'org.webkit:android-jsc:+'

// Load keystore properties for release builds
def keystorePropertiesFile = rootProject.file("../keystore.properties")
def keystoreProperties = new Properties()
if (keystorePropertiesFile.exists()) {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
}

android {
    namespace "com.finchandroid"
    compileSdk rootProject.ext.compileSdkVersion

    buildFeatures {
        buildConfig = true
    }

    lint {
        checkReleaseBuilds false
        abortOnError false
    }

    defaultConfig {
        applicationId "com.finchandroid"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 3
        versionName "1.1.0"
    }
    signingConfigs {
        debug {
            storeFile file('debug.keystore')
            storePassword 'android'
            keyAlias 'androiddebugkey'
            keyPassword 'android'
        }
        release {
            if (keystorePropertiesFile.exists()) {
                storeFile file(keystoreProperties['storeFile'])
                storePassword keystoreProperties['storePassword']
                keyAlias keystoreProperties['keyAlias']
                keyPassword keystoreProperties['keyPassword']
            }
        }
    }
    buildTypes {
        debug {
            signingConfig signingConfigs.debug
        }
        release {
            signingConfig signingConfigs.release
            minifyEnabled enableProguardInReleaseBuilds
            shrinkResources enableProguardInReleaseBuilds
            proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro"
        }
    }
}

apply from: file("../../../../node_modules/react-native-vector-icons/fonts.gradle")
apply from: file("../../../../node_modules/@react-native-community/cli-platform-android/native_modules.gradle"); applyNativeModulesAppBuildGradle(project)

dependencies {
    implementation("com.facebook.react:react-android")
    implementation("com.facebook.react:hermes-android")
    def isGifEnabled = findProperty('reactNativeEnableGif') ?: true
    if (isGifEnabled) {
        implementation 'com.facebook.fresco:animated-gif:2.5.0'
    }
    implementation 'com.facebook.soloader:soloader:0.10.3'

    // Plaid Link SDK is now auto-linked via react-native-plaid-link-sdk

    if (findProperty('FLIPPER_VERSION')) {
        debugImplementation("com.facebook.flipper:flipper:${findProperty('FLIPPER_VERSION')}") {
            exclude group: 'com.facebook.fbjni'
        }
        debugImplementation("com.facebook.flipper:flipper-network-plugin:${findProperty('FLIPPER_VERSION')}") {
            exclude group: 'com.facebook.flipper'
        }
        debugImplementation("com.facebook.flipper:flipper-fresco-plugin:${findProperty('FLIPPER_VERSION')}") {
            exclude group: 'com.facebook.flipper'
        }
    }
}